clear
set more off

global Output_11 "C:/Users/hxu15/Downloads/Output - Version 1.1"
global Output_20 "C:/Users/hxu15/Downloads/EDFactsDatasets/NewOutput"
global Original "C:/Users/hxu15/Downloads/EDFactsDatasets/2022"


foreach s in ela math sci {
	import delimited "${Original}/AZ_EFParticipation_2022_`s'.csv", case(preserve) clear
	save "${Original}/AZ_EFParticipation_2022_`s'.dta", replace
}

use "${Original}/AZ_EFParticipation_2022_ela.dta"
append using "${Original}/AZ_EFParticipation_2022_math.dta" "${Original}/AZ_EFParticipation_2022_sci.dta"


//Rename and Drop Vars
drop SchoolYear State
rename NCESLEAID NCESDistrictID
drop LEA School
rename NCESSCHID NCESSchoolID
rename Value Participation
drop DataGroup DataDescription Denominator Numerator Population
rename Subgroup StudentSubGroup
replace StudentSubGroup = Characteristics if missing(StudentSubGroup) & !missing(Characteristics)
rename AgeGrade GradeLevel
rename AcademicSubject Subject
drop ProgramType Outcome Characteristics

//Clean ParticipationRate
foreach var of varlist Participation {
replace `var' = "*" if `var' == "S"	
gen range`var' = substr(`var',1,1) if regexm(`var',"[<>]") !=0
replace `var' = subinstr(`var', "=","",.)
destring `var', gen(n`var') i(*%<>-)
replace `var' = range`var' + string(n`var'/100, "%9.3g") if `var' != "*" & `var' != "--"
replace `var' = subinstr(`var',">","",.) + "-1" if strpos(`var', ">") !=0
replace `var' = subinstr(`var', "<","0-",.) if strpos(`var', "<") !=0
drop n`var'
drop range`var'
}

//StudentSubGroup
replace StudentSubGroup = "All Students" if strpos(StudentSubGroup, "All Students") !=0
replace StudentSubGroup = "American Indian or Alaska Native" if StudentSubGroup == "American Indian/Alaska Native/Native American"
replace StudentSubGroup = "Black or African American" if StudentSubGroup == "Black (not Hispanic) African American"
replace StudentSubGroup = "Hispanic or Latino" if StudentSubGroup == "Hispanic/Latino"
replace StudentSubGroup = "Native Hawaiian or Pacific Islander" if StudentSubGroup == "Native Hawaiian or Other Pacific Islander"
replace StudentSubGroup = "Two or More" if StudentSubGroup == "Multicultural/Multiethnic/Multiracial/other"
replace StudentSubGroup = "White" if StudentSubGroup == "White or Caucasian (not Hispanic)"
replace StudentSubGroup = "SWD" if StudentSubGroup == "Children with disabilities"
replace StudentSubGroup = "Migrant" if StudentSubGroup == "Migratory students"
replace StudentSubGroup = "Military" if StudentSubGroup == "Military connected"
replace StudentSubGroup = "Foster Care" if StudentSubGroup == "Foster care students"
replace StudentSubGroup = "Asian" if StudentSubGroup == "Asian/Pacific Islander"


//Subject
replace Subject = "ela" if Subject == "Reading/Language Arts"
replace Subject = "math" if Subject == "Mathematics"
replace Subject = "sci" if Subject == "Science"

//GradeLevel
replace GradeLevel = subinstr(GradeLevel, "Grade ", "G0",.)

duplicates drop NCESDistrictID NCESSchoolID GradeLevel Subject StudentSubGroup, force

//Saving EDFacts Output
save "${Original}/AZ_EFParticipation_2022", replace

//Merging with 2022
use "${Output_11}/AZ_AssmtData_2022", clear

//DataLevel
label def DataLevel 1 "State" 2 "District" 3 "School"
encode datalevel, gen(DataLevel_n) label(DataLevel)
sort DataLevel_n 
drop datalevel 
rename DataLevel_n DataLevel

//Convert to numeric if necessary
destring ncesdistrictid ncesschoolid, replace
rename ncesdistrictid NCESDistrictID
rename ncesschoolid NCESSchoolID
rename gradelevel GradeLevel
rename subject Subject
rename studentsubgroup StudentSubGroup
rename state State
rename stateabbrev StateAbbrev
rename statefips StateFips
rename schyear SchYear
rename distname DistName
rename schname SchName
rename stateassigneddistid StateAssignedDistID
rename stateassignedschid StateAssignedSchID
rename assmtname AssmtName
rename assmttype AssmtType
rename studentgroup StudentGroup 
rename studentgroup_totaltested StudentGroup_TotalTested 
rename studentsubgroup_totaltested StudentSubGroup_TotalTested 
rename lev1_count Lev1_count 
rename (lev1_percent lev2_count lev2_percent lev3_count lev3_percent lev4_count lev4_percent lev5_count lev5_percent) (Lev1_percent Lev2_count Lev2_percent Lev3_count Lev3_percent Lev4_count Lev4_percent Lev5_count Lev5_percent)
rename (avgscalescore proficiencycriteria proficientorabove_count proficientorabove_percent flag_assmtnamechange flag_cutscorechange_ela flag_cutscorechange_math flag_cutscorechange_sci flag_cutscorechange_soc)(AvgScaleScore ProficiencyCriteria ProficientOrAbove_count ProficientOrAbove_percent Flag_AssmtNameChange Flag_CutScoreChange_ELA Flag_CutScoreChange_math Flag_CutScoreChange_sci Flag_CutScoreChange_soc) 
rename (disttype distcharter distlocale schtype schlevel schvirtual countyname countycode) (DistType DistCharter DistLocale SchType SchLevel SchVirtual CountyName CountyCode)
rename participationrate ParticipationRate

duplicates drop NCESDistrictID NCESSchoolID GradeLevel Subject StudentSubGroup, force

//Merging
merge 1:1 NCESDistrictID NCESSchoolID GradeLevel Subject StudentSubGroup using "${Original}/AZ_EFParticipation_2022"
drop if _merge ==2
replace ParticipationRate = Participation
replace ParticipationRate = "--" if missing(ParticipationRate)
drop _merge Participation

//Final Cleaning
order State StateAbbrev StateFips SchYear DataLevel DistName SchName NCESDistrictID StateAssignedDistID NCESSchoolID StateAssignedSchID AssmtName AssmtType Subject GradeLevel StudentGroup StudentGroup_TotalTested StudentSubGroup StudentSubGroup_TotalTested Lev1_count Lev1_percent Lev2_count Lev2_percent Lev3_count Lev3_percent Lev4_count Lev4_percent Lev5_count Lev5_percent AvgScaleScore ProficiencyCriteria ProficientOrAbove_count ProficientOrAbove_percent ParticipationRate Flag_AssmtNameChange Flag_CutScoreChange_ELA Flag_CutScoreChange_math Flag_CutScoreChange_sci Flag_CutScoreChange_soc DistType DistCharter DistLocale SchType SchLevel SchVirtual CountyName CountyCode
 
keep State StateAbbrev StateFips SchYear DataLevel DistName SchName NCESDistrictID StateAssignedDistID NCESSchoolID StateAssignedSchID AssmtName AssmtType Subject GradeLevel StudentGroup StudentGroup_TotalTested StudentSubGroup StudentSubGroup_TotalTested Lev1_count Lev1_percent Lev2_count Lev2_percent Lev3_count Lev3_percent Lev4_count Lev4_percent Lev5_count Lev5_percent AvgScaleScore ProficiencyCriteria ProficientOrAbove_count ProficientOrAbove_percent ParticipationRate Flag_AssmtNameChange Flag_CutScoreChange_ELA Flag_CutScoreChange_math Flag_CutScoreChange_sci Flag_CutScoreChange_soc DistType DistCharter DistLocale SchType SchLevel SchVirtual CountyName CountyCode

sort DataLevel DistName SchName Subject GradeLevel StudentGroup StudentSubGroup

save "${Output_20}/AZ_AssmtData_2022", replace
export delimited "${Output_20}/AZ_AssmtData_2022", replace


